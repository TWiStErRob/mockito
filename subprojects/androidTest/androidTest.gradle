// Top-level build file where you can add configuration options common to all sub-projects/modules.

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace = "org.mockitousage.androidtest"
    compileSdk = 33

    defaultConfig {
        minSdk = 26
        targetSdk = 33
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }
}

androidComponents {
    beforeVariants(selector().withBuildType("release")) {
        enable = false
    }
}

apply from: "$rootDir/gradle/dependencies.gradle"

dependencies {
    implementation libraries.kotlin.stdlib

    // Add :android on the classpath so that AGP's jacoco setup thinks it's "production code to be tested".
    runtimeOnly project(":android")
    // Exclude :android from JVM tests, because otherwise it clashes with mockito-core/project(":").
    configurations.testImplementation { exclude group: 'org.mockito', module: 'android' }

    testImplementation project(":")
    testImplementation libraries.junit4
    testImplementation libraries.junitJupiterApi
    testImplementation libraries.junitJupiterEngine

    androidTestImplementation libraries.android.runner
    androidTestImplementation libraries.android.junit
    androidTestImplementation project(":android")
}
