allprojects { project ->
    plugins.withId("java") {
        project.apply plugin: "jacoco"
        project.jacoco {
            toolVersion = '0.8.8'
        }
    }
}

def mockitoCoverage = tasks.register("mockitoCoverage", JacocoReport) { mockitoCoverage ->
    allprojects { currentProject ->
        plugins.withId("jacoco") {
            mockitoCoverage.sourceSets currentProject.sourceSets.main
            Test test = currentProject.tasks.test
            mockitoCoverage.executionData(currentProject.files(test.jacoco.destinationFile).builtBy(test))
        }
    }

    reports {
        xml.required = true
        html.required = true
        html.outputLocation = rootProject.layout.buildDirectory.dir("jacocoHtml")
    }

    doLast {
        def reportFile = new File(reports.html.outputLocation.get().asFile, "index.html")
        logger.lifecycle "Jacoco HTML created: file://${reportFile.toURI().path}"
    }
}

gradle.taskGraph.whenReady {
    mockitoCoverage.configure {
        classDirectories.setFrom(
            classDirectories.files.collect {
                fileTree(
                    dir: it,
                    exclude: ["**/internal/util/concurrent/**"]
                )
            }
        )
    }
}

tasks.register("coverageReport") {
    dependsOn(mockitoCoverage)
}
