allprojects { project ->
    plugins.withId("java") {
        project.apply plugin: "jacoco"
        project.jacoco {
            toolVersion = '0.8.8'
        }
    }
}

def mockitoCoverage = tasks.register("mockitoCoverage", JacocoReport) { mockitoCoverage ->
    allprojects { Project currentProject ->
        plugins.withId("jacoco") {
            // JacocoReport.sourceSets() appends both sourceDirectories and classDirectories.
            mockitoCoverage.sourceSets currentProject.sourceSets.main
            Test test = currentProject.tasks.test
            mockitoCoverage.executionData(currentProject.files(test.jacoco.destinationFile).builtBy(test))
        }
    }

    gradle.taskGraph.whenReady { // Theoretically Gradle.projectsEvaluated, but not possible here.
        // Run this after all projects' sourceSets have been added (which is delayed to when the jacoco plugin is applied).
        classDirectories.setFrom(
            classDirectories.files.collect {
                fileTree(
                    dir: it,
                    exclude: ["**/internal/util/concurrent/**"]
                )
            }
        )
    }

    reports {
        xml.required = true
        html.required = true
        html.outputLocation = rootProject.layout.buildDirectory.dir("jacocoHtml")
    }

    doLast {
        def reportFile = new File(reports.html.outputLocation.get().asFile, "index.html")
        logger.lifecycle "Jacoco HTML created: file://${reportFile.toURI().path}"
    }
}

tasks.register("coverageReport") {
    dependsOn(mockitoCoverage)
}
